buildscript {
    repositories {
        mavenRepo urls: 'http://repo.springsource.org/plugins-snapshot'
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.0.x-SNAPSHOT'
    }
}

allprojects {
    apply plugin: 'artifactory'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'maven'

    repositories {
        mavenRepo urls: 'https://repo.springsource.org/libs-release'
    }

    dependencies {
        groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.3'
        compile gradleApi()
    }
}

project(':gradle-bundlor-plugin') {
    group = 'org.springframework.build.bundlor'
    version = '1.0.0-SNAPSHOT'
}

project(':gradle-docbook-plugin') {
    group = 'org.springframework.build.docbook'
    version = '1.0.0-SNAPSHOT'
    dependencies {
        compile 'org.apache.xerces:resolver:2.9.1',
                'saxon:saxon:6.5.3',
                'org.apache.xerces:xercesImpl:2.9.1',
                'net.sf.xslthl:xslthl:2.0.1',
                'net.sf.docbook:docbook-xsl:1.75.2:resources@zip',
                // FOP dependencies
                'org.apache.xmlgraphics:fop:0.95-1@jar',
                'org.apache.xmlgraphics:xmlgraphics-commons:1.3',
                'org.apache.xmlgraphics:batik-bridge:1.7@jar',
                'org.apache.xmlgraphics:batik-util:1.7@jar',
                'org.apache.xmlgraphics:batik-css:1.7@jar',
                'org.apache.xmlgraphics:batik-dom:1.7',
                'org.apache.xmlgraphics:batik-svg-dom:1.7@jar',
                'org.apache.avalon.framework:avalon-framework-api:4.3.1'
    }
}

repoUser = project.properties['REPO_USERNAME']
repoPass = project.properties['REPO_PASSWORD']

gradle.taskGraph.whenReady {taskGraph ->
    if (taskGraph.hasTask(':artifactoryPublish') &&
            (repoUser == null || repoPass == null)) {
        throw new InvalidUserDataException(
            "Please provide REPO_USERNAME and REPO_PASSWORD properties")
    }
}

artifactory {
    contextUrl = 'https://repo.springsource.org'
    publish {
        repository {
            repoKey = 'plugins-snapshot-local'
            username = repoUser
            password = repoPass
        }
        defaults {
            publishConfigs('archives')
        }
    }
    resolve {
        repository {
            repoKey = 'libs-releases'
            username = repoUser
            password = repoPass
            maven = true
        }
    }
}

task wrapper(type:Wrapper) {
    jarFile = ".wrapper/gradle-wrapper.jar";
    gradleVersion='1.0-milestone-5'
}
